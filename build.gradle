import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id("java")
    id("com.vanniktech.maven.publish") version "0.28.0"
    id("signing")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

signing {
    useInMemoryPgpKeys(
        System.getenv("GPG_PRIVATE_KEY"),
        System.getenv("GPG_PASSPHRASE")
    )
    sign(publishing.publications)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.lowagie:itext:2.0.7'
    implementation 'org.apache.pdfbox:pdfbox:2.0.29'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

tasks.test {
    useJUnitPlatform()
}

mavenPublishing {
    coordinates("io.github.haeyonghahn", "converter-to-pdf", "0.0.4")

    pom {
        name.set(project.rootProject.name)
        description.set("Library that converts files to PDF")
        inceptionYear.set("2024")
        url.set("https://github.com/haeyonghahn/converter-to-pdf.git")

        licenses {
            license {
                name.set("MIT License")
                url.set("https://github.com/haeyonghahn/converter-to-pdf.git")
                distribution.set("repo")
            }
        }

        developers {
            developer {
                id.set("haeyonghahn")
                name.set("HaeYong Hahn")
                email.set("yong80211@gmail.com")
            }
        }

        scm {
            connection.set("scm:git:git://github.com/haeyonghahn/converter-to-pdf.git")
            developerConnection.set("scm:git:ssh://git@github.com:haeyonghahn/converter-to-pdf.git")
            url.set("https://github.com/haeyonghahn/converter-to-pdf.git")
        }
    }

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
}